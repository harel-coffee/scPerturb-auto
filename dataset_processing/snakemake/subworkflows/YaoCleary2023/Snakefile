"""
Author: Stefan Peidli
Date: 22.11.2023
Run: snakemake
"""

from pathlib import Path
configfile: "../../configuration/config.yaml"

### PATHS ###
DATADIR = Path(config['DOWNDIR'])  # place to store data
TEMPDIR = Path(config['TEMPDIR'])  # place to store temporary files (huge files)

keys = ['GSM6858447_KO_conventional', 'GSM6858449_KD_conventional', 'GSM6858448_KO_cell_pooled', 'GSM6858450_KD_guide_pooled']

### RULES ###
rule YaoCleary2023_download:
    output: 
        temp(expand(TEMPDIR / 'YaoCleary2023/{file}.rds', file=keys)),
        temp(expand(TEMPDIR / 'YaoCleary2023/{file}_perturbations.txt', file=keys)),
        temp(expand(TEMPDIR / 'YaoCleary2023/{file}_FRPerturb_effect_sizes.csv', file=keys))
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=64000,
        disk_mb=64000
    shell:
        """
        rm -rf {TEMPDIR}/YaoCleary2023/*
        cd {TEMPDIR}/YaoCleary2023
        wget -O GSE221321_RAW.tar 'http://www.ncbi.nlm.nih.gov/geo/download/?acc=GSE221321&format=file'
        tar -xvf GSE221321_RAW.tar
        rm GSE221321_RAW.tar
        gunzip *.gz
        """

rule YaoCleary2023_extract:
    conda:  'r_env' # '../../../../environments/r_env.yaml'
    input: TEMPDIR / 'YaoCleary2023/{file}.rds'
    output: 
        temp(TEMPDIR / 'YaoCleary2023/{file}_matrix.mtx'),
        temp(TEMPDIR / 'YaoCleary2023/{file}_genes.csv'),
        temp(TEMPDIR / 'YaoCleary2023/{file}_barcodes.csv'),
        temp(TEMPDIR / 'YaoCleary2023/{file}_metadata.csv'),
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=128000,
        disk_mb=128000
    shell: "Rscript ../utils/extract.R -f {input} -n {wildcards.file} -o $(dirname {output[0]})"

rule YaoCleary2023_merge:
    input: 
        TEMPDIR / 'YaoCleary2023/{file}_matrix.mtx',
        TEMPDIR / 'YaoCleary2023/{file}_genes.csv',
        TEMPDIR / 'YaoCleary2023/{file}_metadata.csv'
    output: temp(TEMPDIR / 'YaoCleary2023/{file}_temp.h5ad')
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=128000,
        disk_mb=128000
    shell: "python ../utils/merge_from_extract.py -i $(dirname {input[0]}) -n {wildcards.file} -o {output}"

rule YaoCleary2023:
    input: expand(TEMPDIR / 'YaoCleary2023/{file}_temp.h5ad', file=keys)
    output: DATADIR / 'YaoCleary2023.h5ad'
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=128000,
        disk_mb=128000
    script: "YaoCleary2023.py"