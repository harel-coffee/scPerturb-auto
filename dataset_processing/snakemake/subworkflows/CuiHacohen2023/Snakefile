"""
Author: Stefan Peidli
Date: 12.01.2023
Run: snakemake
"""

from pathlib import Path
configfile: "../../configuration/config.yaml"

### PATHS ###
DATADIR = Path(config['DOWNDIR'])  # place to store data
TEMPDIR = Path(config['TEMPDIR'])  # place to store temporary files (huge files)

### SAMPLES ###
celltypes = [
    'B_cell',
    'cDC1',
    'cDC2',
    'eTAC',
    'ILC',
    'Macrophage',
    'MigDC',
    'Monocyte',
    'Neutrophil',
    'NK_cell',
    'pDC',
    'T_cell_CD4',
    'T_cell_CD8',
    'T_cell_gd',
    'Treg'
]

# ### RULES ###
# You will manually have to download the rds files.
# Cannot be automatized due to JavaScript on the website.
rule CuiHacohen2023_extract:
    input:
        TEMPDIR / 'CuiHacohen2023/ref_data_{celltype}.RDS'
    output:
        temp(TEMPDIR / 'CuiHacohen2023/{celltype}_temp.h5ad')
    conda: 'r_env' # '../../../../environments/r_env.yaml'
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=128000,
        disk_mb=64000
    shell:
        """
        Rscript ../utils/extract.R -f {input} -n {wildcards.celltype} -o $(dirname {output[0]})
        python ../utils/merge_from_extract.py -i $(dirname {input[0]}) -o {output} -n {wildcards.celltype}
        """

rule CuiHacohen2023:
    input: 
        expand(TEMPDIR / 'CuiHacohen2023/{celltype}_temp.h5ad', celltype=celltypes)
    output:
        DATADIR / 'CuiHacohen2023.h5ad'
    resources:
        partititon='short',
        time='04:00:00',
        mem_mb=128000,
        disk_mb=64000
    script: 'CuiHacohen2023.py'
